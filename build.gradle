plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id "org.jlleitschuh.gradle.ktlint" version "10.1.0"
    id "antlr"
    id "idea"
}

repositories {
    mavenCentral()
    mavenLocal()
}

def generatedMain = "generated-src/antlr/main"
def generatedMainFile = file(generatedMain)

dependencies {
    antlr "org.antlr:antlr4:$antlrVersion"
    implementation "org.antlr:antlr4-runtime:$antlrVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation "org.apache.logging.log4j:log4j-api-kotlin:1.0.0"
    implementation "org.apache.logging.log4j:log4j-api:2.12.1"
    implementation "org.apache.logging.log4j:log4j-core:2.12.1"

    implementation 'com.strumenta.kolasu:kolasu-core:1.3.3'

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

configurations.all() {
    resolutionStrategy.force "org.antlr:antlr4-runtime:$antlrVersion"
    resolutionStrategy.force "org.antlr:antlr4-tool:$antlrVersion"
}

generateGrammarSource {
    inputs.files fileTree('src/main/antlr').include('**/*.g4')
    maxHeapSize = "64m"
    arguments += ['-package', 'com.strumenta.python3parser']
    outputDirectory = file(generatedMain + "/com/strumenta/python3parser")
}

compileJava {
    sourceCompatibility = "$jvmVersion"
    targetCompatibility = "$jvmVersion"
    dependsOn generateGrammarSource
    source generatedMainFile, sourceSets.main.java
}

compileTestKotlin {
    sourceCompatibility = "$jvmVersion"
    targetCompatibility = "$jvmVersion"
    kotlinOptions.jvmTarget = "$jvmVersion"
}

compileKotlin {
    sourceCompatibility = "$jvmVersion"
    targetCompatibility = "$jvmVersion"
    source generatedMainFile, sourceSets.main.java, sourceSets.main.kotlin
    kotlinOptions.jvmTarget = "$jvmVersion"
    dependsOn generateGrammarSource
}

clean {
    delete file(generatedMain)
    mkdir generatedMain
}

idea {
    module {
        mkdir generatedMain
        sourceDirs += generatedMainFile
        println sourceDirs
    }
}

ktlint {
    version = "0.41.0"
    verbose = true
    outputToConsole = true
    enableExperimentalRules = true
    disabledRules = ["no-wildcard-imports", "experimental:argument-list-wrapping"]
}
